rootProject.name = 'spring-cloud' // Root Project명 명시

def subModuleDirectoryName = ["service"]
subModuleDirectoryName.each {

    def compDir = new File(rootDir, it)
    if(!compDir.exists()){ // subModuleDirectoryName에 해당하는 directory가 없는 경우 rootDir 경로에 directory 생성
        compDir.mkdirs()
    }

    compDir.eachDir {subDir ->

        /**
         * subModuleDirectoryName에 명시한 각 directory의 하위 directory에 sub-module-project 구성할 것이므로
         * 각 sub-module-project에 build.gradle이 없는 경우 subDir.absolutePath 경로에 build.gradle 생성
         */
        def gradleFile = new File(subDir.absolutePath, "build.gradle")
        if(!gradleFile.exists()){
            gradleFile.text =
                    """
                    dependencies {

                    }
                    """.stripIndent(20)
        }

        def projectResourceDirectories = [
                "src/main/java/springcloud",
                "src/main/resources",
                "src/test/java/springcloud",
                "src/test/resources"
        ]
        projectResourceDirectories.each {srcDir->
            def srcFolder = new File(subDir.absolutePath, srcDir)
            if(!srcFolder.exists()){ // projectResourceDirectories에 해당하는 directory가 없는 경우 subDir.absolutePath 경로에 생성
                srcFolder.mkdirs()
            }
        }

        def projectName = ":${it}-${subDir.name}"
        include projectName
        project(projectName).projectDir = subDir
    }
}

include("gateway")
include("eureka")